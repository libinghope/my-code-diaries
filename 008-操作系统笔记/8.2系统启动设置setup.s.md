---
title: 系统启动设置setup.s
date: 2020/2/16
comments: true
categories:
  - 操作系统
tags:
  - 必备
  - 技能
permalink: "3.2"
description: 把第2个(从1开始)到第5个扇区，共四个扇区的信息读入内存并执行
---
接着上一篇笔记  
bootsect.s从磁盘中把第2到5个扇区的setup.s程序读入到内存0x90200地址处。

setup.s做了以下这些事情

## 1、读取硬件信息生成硬件信息表

|内存地址|名称|长度|描述|
|:------:|:---:|:---------------:|:--------------------------------:|
|0x90000  |  2  |光标位置          |列号(最左边：0x00),行号(最上边:0x00)
|0x90002  |  2  |扩展内存参数       |系统从1M开始的扩展内存参数
|0x90004  |  2  |显示页面          |当前显示页面
|0x90006  |  1  |显示模式          |···
|0x90007  |  2  |字符列数          |···
|···      | ··  |                 |···
|0x9000A  |  1  |显示内存          |显存大小
|0x9000B  |  1  |显示状态          |0x00-彩色0x3dx  0x11单色0x3bx
|0x9000C  |  2  |显卡特性参数       |显存大小
|···      |···  |···              |···
|0x90080  |  16 |硬盘参数表        |第一个硬盘的参数
|0x90090  |  16 |硬盘参数表        |第二个硬盘的参数(没有的话就是0)
|0x901FC  |  2  |根设备号          |根文件所在的设备号(bootsect.s中设置的)

## 2、把system代码移动到0地址处

总共移动8次，每次移动64k,共512k,覆盖掉了原来的BIOS中断向量表(用不到了)
详情如下面代码所示

```ASM
INITSEG  = 0x9000    ！这个地址开始存放系统硬件信息
SYSSEG   = 0x1000    ！system 存放的地方 0x10000 (65536).
SETUPSEG = 0x9020    ! setup.s存放的地址

.globl begtext, begdata, begbss, endtext, enddata, endbss
.text
begtext:
.data
begdata:
.bss
begbss:
.text

entry start
start:
;保存当前光标位置
    mov    ax,#INITSEG
    mov    ds,ax            ;ds设置成INITSEG
    mov    ah,#0x03         ;int 10读光标功能号3
    xor    bh,bh
    int    0x10             ;调用中断，读取光标位置
    mov    [0],dx           ;光标信息存在dx中，存入0x90000处
    mov    ah,#0x88         ;int 15取扩展内存大小功能号0x88
    int    0x15             ;调用中断
    mov    [2],ax           ;返回从100000开始的扩展内存大小信息存入0x90002处

;保存显卡当前显示模式
    mov    ah,#0x0f
    int    0x10
    mov    [4],bx           ; bh = display page
    mov    [6],ax           ; al = video mode, ah = window width

;检查显示方式(EGA/VGA),并选取参数
    mov    ah,#0x12
    mov    bl,#0x10
    int    0x10
    mov    [8],ax
    mov    [10],bx          ;显示内存，显示状态
    mov    [12],cx          ;显卡特性参数

;取第0个硬盘信息
    mov    ax,#0x0000
    mov    ds,ax
    lds    si,[4*0x41]      ;取中断向量，41的值,即硬盘0参数表的地址
    mov    ax,#INITSEG
    mov    es,ax
    mov    di,#0x0080       ;传输向量表到达的目的地址:9000:0080
    mov    cx,#0x10         ;取10字节
    rep
    movsb                   ;循环复制

;取第一个磁盘信息
    mov    ax,#0x0000
    mov    ds,ax
    lds    si,[4*0x46]      ;取中断向量，46的值,即硬盘1参数表的地址
    mov    ax,#INITSEG
    mov    es,ax
    mov    di,#0x0090       ;传输向量表到达的目的地址:9000:0090
    mov    cx,#0x10         ;取10字节
    rep
    movsb

;检查是否存在第二个硬盘
    mov    ax,#0x01500
    mov    dl,#0x81
    int    0x13
    jc     no_disk1        ;如果cf==1,跳转,没有第二个磁盘
    cmp    ah,#3           ;判断是否有硬盘
    je     is_disk1

;没有,则删除第二个硬盘表
no_disk1:
    mov    ax,#INITSEGjj
    mov    es,ax
    mov    di,#0x0090
    mov    cx,#0x10
    mov    ax,#0x00
    rep
    stosb

is_disk1:

;为开始保护模式做准备
    cli            ;不允许中断

;首先我们将系统模块移动到新的目标位置0x0000
    mov    ax,#0x0000
    cld                     ; 'direction'=0, 指明复制方向 从低到高
do_move:
    mov    es,ax            ;此时ax=0x0000,被复制到的目的地址es:di=>0000:0
    add    ax,#0x1000       ;每次移动0x10000()个字节
    cmp    ax,#0x9000
    jz     end_move
    mov    ds,ax            ;原地址ds:si=>1000:0 2000:0 3000:0···
    sub    di,di            ;实模式下最大偏移是64k,所以一次最多复制这么多
    sub    si,si            ;第一次放在0-0x10000处，第二次0x10000-0x20000···
    mov    cx,#0x8000       ;总共移动了0x8000(32768)字,即0x10000Byte,(65536/1024=64k)
    rep                     ; 1000:0->000:0,2000:0->1000:0···
    movsw
    jmp    do_move

;setup.s中给出了中断描述表和全局描述符表。在这里把它们的首地址放入指定寄存器中
;这是专用寄存器
end_move:
    mov    ax,#SETUPSEG     ; 0x9020
    mov    ds,ax            ;ds指向setup段
    lidt   idt_48           ;加载中断描述符表寄存器
    lgdt   gdt_48           ;加载全局描述符表寄存器

;开启A20地址线，准备进入32位寻址模式
    call    empty_8042
    mov     al,#0xD1        ; 写指令
    out     #0x64,al
    call    empty_8042
    mov     al,#0xDF        ; A20 on开启32位寻址模式
    out     #0x60,al
    call    empty_8042

    mov     al,#0x11        ; initialization sequence
    out     #0x20,al        ; send it to 8259A-1
    .word   0x00eb,0x00eb   ; jmp $+2, jmp $+2,$表示当前指令地址
    out     #0xA0,al        ; and to 8259A-2
    .word   0x00eb,0x00eb
    mov     al,#0x20        ; start of hardware int's (0x20)
    out     #0x21,al
    .word   0x00eb,0x00eb
    mov     al,#0x28        ; start of hardware int's 2 (0x28)
    out     #0xA1,al
    .word   0x00eb,0x00eb
    mov     al,#0x04        ; 8259-1 is master
    out     #0x21,al
    .word   0x00eb,0x00eb
    mov     al,#0x02        ; 8259-2 is slave
    out     #0xA1,al
    .word   0x00eb,0x00eb
    mov     al,#0x01        ; 8086 mode for both
    out     #0x21,al
    .word   0x00eb,0x00eb
    out     #0xA1,al
    .word   0x00eb,0x00eb
    mov     al,#0xFF        ; mask off all interrupts for now
    out     #0x21,al
    .word   0x00eb,0x00eb
    out     #0xA1,al

    mov     ax,#0x0001      ; 开启保护模式
    lmsw    ax              ; This is it!
    jmpi    0,8             ; 跳到描述符表的第一项 head.s
;jmpi指令解释：段间跳转，参数1:偏移量，参数2：段选择子
;跳转到8:0位置,这里的8为实模式下的段选择符，目的地址是0x00000000
;关于8的解析：这里的8对应二进制的"1000",这后两位0表示内核特权级，倒数第三位的0表示gdt
;1表示用全局描述符表的第1项,所以该项指出代码的基地址是0,也就是接下来执行的head.s

;检查键盘命令队列是否为空，当输入缓冲器为空则可以对其进行写命令
empty_8042:
    .word   0x00eb,0x00eb
    in      al,#0x64        ; 8042 status port
    test    al,#2           ; is input buffer full?
    jnz     empty_8042      ; yes - loop
    ret

;全局描述符表开始处
gdt:
    .word    0,0,0,0        ; dummy//第一描述符，不用

;这里在gdt表中的偏移量为08,联系我们上面的jmpi 0,8，也就是调用此处的表内容
;加载代码段寄存器时，使用这个偏移
    .word    0x07FF         ; 8Mb - limit=2047 (2048*4096=8Mb)
    .word    0x0000         ; base address=0
    .word    0x9A00         ; code read/exec
    .word    0x00C0         ; granularity=4096, 386

;这里在gdt表中的偏移量是0x10,当加载数据段寄存器时,使用这个偏移
    .word    0x07FF         ; 8Mb - limit=2047 (2048*4096=8Mb)
    .word    0x0000         ; base address=0
    .word    0x9200         ; data read/write
    .word    0x00C0         ; granularity=4096, 386

idt_48:
    .word    0              ; idt limit=0
    .word    0,0            ; idt base=0L

gdt_48:
    .word    0x800          ; gdt limit=2048, 256 GDT entries
    .word    512+gdt,0x9    ; gdt base = 0X9xxxx
.text
endtext:
.data
enddata:
.bss
endbss:
```
